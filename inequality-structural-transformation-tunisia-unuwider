* Simone Schotte, UNU-WIDER
* Data Cleaning Code
* Ghana - GLSS
* June 2019

clear all
set more off, perm
global data "C:\Users\Dell\Downloads\Tunisie - UNUWider\example dofile\Ghana\Data"
global dataGLSS "C:\Users\Dell\Downloads\Tunisie - UNUWider\example dofile\Ghana\Data" 

/****************
*** 1998/1999 ***
*****************

use "$data\GLSS4_1998\DATA\SEC4A.DTA", clear

gen wave = 4

* Employment status & Employment type
recode s4aq1 (1=1)(2=0), gen(empl_we)
recode s4aq2 (1=1)(2=0), gen(empl_se)
recode s4aq3 (1=1)(2=0), gen(empl_fa)
recode s4aq4 (1=1)(2=0), gen(empl_up)

egen empl=rowtotal(empl_we empl_se empl_fa empl_up)
lab var empl "Employment status"
tab empl

gen empl_type = . 
replace empl_type = 1 if empl_we == 1
replace empl_type = 2 if empl_se == 1
replace empl_type = 3 if empl_fa == 1
replace empl_type = 4 if empl_up == 1
lab def empl_type 1 "WAGE/SALARY" 2 "SELF-EMPLOYMENT" 3 "FARM ENTERPRISE" 4 "FAMILY HELP"
lab var empl_type "Employment type"

* Occupation & Activity 
tab s4aq5 // 2-digit ISCO-68?? --> Maybe try to harmonize later...
*/

*****************
*** 2005/2006 ***
*****************

tempfile GLSS5_0 GLSS5_1 GLSS5_2 GLSS5_4

*** GENERAL
use "$data\GLSS5_2005\DATA\PartA\sec0.dta", clear
gen year = ydate
keep clust hhid region ez nh intcode ddate mdate ydate dwell lang weight year
order clust hhid region ez nh weight year intcode ddate mdate ydate dwell lang 
save `GLSS5_0'

*** DEMOGRAPHICS
use "$data\GLSS5_2005\DATA\PartA\sec1.dta", clear
ren s1q2 sex
ren s1q3 rel
gen head = rel == 1
ren s1q4dd borndd
ren s1q4mm bornmm
ren s1q4yy bornyy
ren s1q5y age
ren s1q5m agem
ren s1q6 marital
ren s1q7 spouse
ren s1q8 spouse_id
ren s1q10 religion
ren s1q11 born_region
ren s1q12 national
recode s1q13 (0/19=1 "Ashanti-Akan")(60/69=2 "Mole-Dagbon")(30/39=3 "Ewe")(20/29=4 "Ga-Dangme")(50/59=5 "Gurma")(40/49=6 "Guan")(70/79=7 "Grusi")(80/89=8 "Mande")(90/99=9 "Other Groups"), gen(ethnic)
keep if s1q24==1 // household member
bys hhid nh: egen hhsize = total(s1q24)
keep hhid nh pid sex rel borndd bornmm bornyy age agem marital spouse spouse_id born_region national ethnic head hhsize
order hhid nh pid sex rel head hhsize borndd bornmm bornyy age agem marital spouse spouse_id born_region national ethnic
save `GLSS5_1'

*** EDUCATION
use "$data\GLSS5_2005\DATA\PartA\sec2a.dta", clear
ren s2aq1 edu_ever
ren s2aq2 edu_grde5
recode edu_grde5 (0=0 "none")(1/15=1 "preschool")(16/18=2 "primary")(19=3 "lower-secondary") ///
	(20/22=2)(23/25=3)(26=4 "upper-secondary")(27/28=2)(29/30=3)(31/33=4)(41/43=5 "post-secondary")(51/53=6 "tertiary")(61=.), gen(edu_lvl)
	replace edu_lvl = 0 if edu_ever == 2
ren s2aq3 edu_qual
ren s2aq5 edu_curr
keep hhid nh pid edu*
save `GLSS5_2' 

*** EMPLOYMENT
use "$data\GLSS5_2005\DATA\PartA\sec4a.dta", clear
ren s4aq1 work 
ren s4aq2 jobs
ren s4aq7 hrs
replace hrs = . if hrs>168
gen days = round(hrs,8)
recode days (0=0)(8=1)(16=2)(24=3)(32=4)(40=5)(48=6)(56/168=7)
replace days = 1 if hrs>0 & hrs<8
order hrs days, after(jobs)
ren s4aq4 isco88_3
replace isco88_3 = 516 if isco88_3==14 | isco88_3==21
merge m:1 isco88_3 using "$data\ISCO_88_3.dta"
replace isco88_3 = . if _merge==1
drop if _merge==2
drop isco88_3_title _merge
recode s4aq5 (111/999=1   "Agriculture and fishing")                          ///
			 (1000/1499=3  "Mining and quarrying")                               ///
			 (1500/3799=4  "Manufacturing")                                      ///
			 (4000/4199=5  "Electricity, gas and water supply")                  ///
			 (4500/4599=6  "Construction")                                       ///
			 (5000/5499=7  "Wholesale and retail trade")                         ///
			 (5500/5599=8  "Hotels and restaurants ")                            ///
			 (6000/6499=9  "Transport, storage and communications")              ///
			 (6500/6799=10 "Financial intermediation")                           ///
			 (7000/7499=11 "Real estate, renting and business activities")       ///
			 (7500/7599=12 "Public administration and defence")                  ///
			 (8000/8099=13 "Education")                                          ///
			 (8500/8599=14 "Health and social work")                             ///
			 (9000/9399=15 "Other services")                                     ///
			 (9500/9799=16 "Private households")                                 ///
			 (9900     =17 "Extraterritorial organizations and bodies")          ///
			 (9980/9999=.), gen(industry)
order industry, after(isco88_3)
ren s4aq8 pay
ren s4aq9a pay_amount
replace pay_amount = pay_amount/10000 // 1 third cedi = 10,000 second cedis
ren s4aq9b pay_time
gen earn = .
replace earn = pay_amount * days if pay_time == 1  // daily
replace earn = pay_amount        if pay_time == 2  // weekly
replace earn = pay_amount / 2    if pay_time == 3  // forthnightly
replace earn = pay_amount / 4.33 if pay_time == 4  // monthly
replace earn = pay_amount / 13   if pay_time == 5  // quarterly
replace earn = pay_amount / 52   if pay_time == 6  // yearly
lab var earn "weekly labour earnings"
order earn, after(pay_time)
ren s4aq11 status
ren s4aq12 sector
ren s4aq15 contract
ren s4aq16 union
ren s4aq17 paid_holiday
ren s4aq18 paid_leave
ren s4aq19 pension
ren s4aq20 medcare
ren s4aq21 socsec 
ren s4aq26 numemp
keep hhid nh pid work jobs hrs isco88_3 industry pay pay_amount pay_time earn status sector contract union paid_holiday paid_leave pension medcare socsec
save `GLSS5_4' 

*** Combine
use `GLSS5_0', clear
merge 1:m hhid nh using `GLSS5_1', nogen
merge 1:1 hhid nh pid using `GLSS5_2', nogen
merge 1:1 hhid nh pid using `GLSS5_4', nogen
merge m:1 isco88_3 using "$data\ISCO_88_3.dta", keepusing(isco88_3) nogen
replace weight=1 if weight==.
gen wave = 5
order wave
save "$data\GLSS5.dta", replace


*****************
*** ISCO88-08 ***
***************** 

tempfile ISCO88_3

gen obs=1
gen wgt=weight/hhsize
collapse (sum) obs wgt, by(isco88_3)
drop if isco88_3 == .
replace wgt=round(wgt)
save `ISCO88_3'

use "$data\ISCO_88_08.dta", clear
keep isco08_4 isco08_4_title isco88_3 isco88_3_title
duplicates drop
merge m:1 isco88_3 using `ISCO88_3', nogen

duplicates report isco08_4
bys isco08_4: egen maxwgt=max(wgt)
keep if wgt == maxwgt
duplicates report isco08_4

count 
set obs 437
replace isco88_3 = . in 437
save "$data\ISCO_08_4", replace


*****************
*** 2012/2013 ***
*****************

tempfile GLSS6_0 GLSS6_1 GLSS6_2 GLSS6_4

*** GENERAL
use "$data\GLSS6_2012-2013\DATA\PartA\g6loc_edt.dta", clear
ren loc2 urban
recode ez (4=1)
merge 1:1 HID using "$data\GLSS6_2012-2013\DATA\PartA\SEC0.dta", nogen
gen hhid = substr(HID,1,5)+substr(HID,-2,.)
destring hhid, replace
replace year = ydate if year != ydate & ydate != . 
keep clust HID hhid region ez urban nh intcode ddate mdate ydate dwell lang weight year
order clust HID hhid region ez urban nh weight year intcode ddate mdate ydate dwell lang 
save `GLSS6_0'

*** DEMOGRAPHICS
use "$data\GLSS6_2012-2013\DATA\PartA\SEC1.dta", clear
ren s1q2 sex
ren s1q3 rel
recode rel (11=10)
gen head = rel == 1
ren s1q4dd borndd
ren s1q4mm bornmm
ren s1q4yy bornyy
ren s1q5y age
ren s1q5m agem
ren s1q6 marital
ren s1q7 spouse
ren s1q8 spouse_id
ren s1q10 religion
ren s1q11 born_region
ren s1q12 national
recode s1q13a (0/19=1 "Ashanti-Akan")(60/69=2 "Mole-Dagbon")(30/39=3 "Ewe")(20/29=4 "Ga-Dangme")(50/59=5 "Gurma")(40/49=6 "Guan")(70/79=7 "Grusi")(80/89=8 "Mande")(90/99=9 "Other Groups"), gen(ethnic)
keep if s1q24==1 // household member
bys HID nh: egen hhsize = total(s1q24)
ren PID pid
keep HID nh pid sex rel borndd bornmm bornyy age agem marital spouse spouse_id born_region national ethnic head hhsize
order HID nh pid sex rel head hhsize borndd bornmm bornyy age agem marital spouse spouse_id born_region national ethnic
save `GLSS6_1'

*** EDUCATION
use "$data\GLSS6_2012-2013\DATA\PartA\sec2a.dta", clear
ren s2aq1 edu_ever
ren s2aq2 edu_grde6
recode edu_grde6 (0=0 "none")(1/15=1 "preschool")(16/18=2 "primary")(19=3 "lower-secondary") ///
	(20/22=2)(23/25=3)(26/27=4 "upper-secondary")(28/29=2)(30/31=3)(32/34=4)(41/43=5 "post-secondary")(51/53=6 "tertiary")(61=.), gen(edu_lvl)
	replace edu_lvl = 0 if edu_ever == 2
ren s2aq3 edu_qual
ren s2aq5 edu_curr
ren PID pid
keep HID nh pid edu*
save `GLSS6_2' 

*** EMPLOYMENT
use "$data\GLSS6_2012-2013\DATA\PartA\sec4a.dta", clear
ren s4aq1 work 
ren s4aq4 jobs
ren s4aq9 hrs
replace hrs = . if hrs>168
gen days = round(hrs,8)
recode days (0=0)(8=1)(16=2)(24=3)(32=4)(40=5)(48=6)(56/168=7)
replace days = 1 if hrs>0 & hrs<8
order hrs days, after(jobs)
corr hrs s4aq5
ren s4aq6 isco08_4
replace isco08_4 = 5212 if isco08_4==7517 // Local Food preparers (raw and cooked)
replace isco08_4 = 5211 if isco08_4==7518 // Local alcoholic and non alcoholic beverages
merge m:1 isco08_4 using "$data\ISCO_08_4.dta"
replace isco08_4 = . if _merge==1
drop if _merge==2
drop isco88_3_title isco08_4_title obs wgt maxwgt _merge
order isco88_3, after(isco08_4)
recode s4aq7 (111/499=1    "Agriculture and fishing")                          ///
			 (500/999=3    "Mining")                                           ///
			 (1000/3399=4  "Manufacturing")                                    ///
			 (3500/3999=5  "Electricity, gas and water supply")                ///
			 (4100/4399=6  "Construction")                                     ///
			 (4500/4799=7  "Wholesale and retail trade")                       ///			  
			 (5500/5699=8  "Hotels and restaurants ")                          ///
			 (4900/5399=9  "Transport, storage and communications")            ///			  
			 (5800/6399=9  "Transport, storage and communications")            ///			  
			 (6400/6699=10 "Financial intermediation")                         ///
			 (6800/6899=11 "Real estate, renting and business activities")     ///			  
			 (8400/8499=12 "Public administration and defence")                ///			  
			 (8500/8599=13 "Education")                                        ///			  
			 (8600/8899=14 "Health and social work")                           ///	
			 (6900/7599=15 "Other services")                                   ///		  
			 (7700/8299=15 "Other services")                                   ///	
			 (9000/9399=15 "Other services")                                   ///	
			 (9400/9699=15 "Other services")                                   ///			  			  
			 (9700/9899=16 "Private households")                               ///			  
			 (9900=17      "Extraterritorial organizations and bodies")        ///
			 (9998/9999=.),gen(industry)
order industry, after(isco88_3)
ren s4aq17 pay
ren s4aq18a pay_amount
ren s4aq18b pay_time
gen earn = .
replace earn = pay_amount * days if pay_time == 1  // daily
replace earn = pay_amount        if pay_time == 2  // weekly
replace earn = pay_amount / 2    if pay_time == 3  // forthnightly
replace earn = pay_amount / 4.33 if pay_time == 4  // monthly
replace earn = pay_amount / 13   if pay_time == 5  // quarterly
replace earn = pay_amount / 52   if pay_time == 6  // yearly
lab var earn "weekly labour earnings"
order earn, after(pay_time)
recode s4aq20 (1=1 "paid employee")(2=2 "nagric with employees")(3=3 "nagric without employees") ///
	(4=4 "unpaid fam worker")(5=5 "agric with employees")(6=6 "agric without employees") ///
	(7=7 "agric unpd fwork") (8=8 "domestic employee")(9=1)(10=9 "apprentice")(11=10 "other"), gen(status)
order status, after(s4aq20)
ren s4aq21 sector
ren s4aq24 contract
ren s4aq25 union
ren s4aq26 paid_holiday
ren s4aq27 paid_leave
ren s4aq28 pension
ren s4aq29 medcare
ren s4aq30 socsec 
ren s4aq35 numemp
ren PID pid
keep HID nh pid work jobs hrs isco88_3 isco08_4 industry pay pay_amount pay_time earn status sector contract union paid_holiday paid_leave pension medcare socsec
save `GLSS6_4' 

*** Combine
use `GLSS6_0', clear
merge 1:m HID nh using `GLSS6_1', nogen
merge 1:1 HID nh pid using `GLSS6_2', nogen
merge 1:1 HID nh pid using `GLSS6_4'
drop if _merge==2
drop _merge
merge m:1 isco88_3 using "$data\ISCO_88_3.dta", keepusing(isco88_3) nogen
replace weight=1 if weight==.
gen wave = 6
order wave
save "$data\GLSS6.dta", replace


*****************
*** 2016/2017 ***
*****************

tempfile GLSS7_0 GLSS7_1 GLSS7_2 GLSS7_4

*** GENERAL
use"$data\GLSS7_2017\DATA\PartA\g7loc_upd.dta", clear
recode ez (4=1)
ren WTA_S weight
ren loc2 urban
merge 1:1 hid using "$data\GLSS7_2017\DATA\PartA\g7sec0.dta", nogen
ren hid HID
gen hhid = substr(HID,1,5)+substr(HID,-2,.)
destring hhid, replace
replace year = ydate if year != ydate & ydate != . 
keep clust HID hhid region ez urban nh /*intcode*/ ddate mdate ydate dwell lang weight year
order clust HID hhid region ez urban nh weight year /*intcode*/ ddate mdate ydate dwell lang 
save `GLSS7_0'

*** DEMOGRAPHICS
use "$data\GLSS7_2017\DATA\PartA\g7sec1.dta", clear
ren s1q2 sex
ren s1q3 rel
recode rel (11=10)
gen head = rel == 1
ren s1q4dd borndd
ren s1q4mm bornmm
ren s1q4yy bornyy
ren s1q5y age
ren s1q5m agem
ren s1q6 marital
ren s1q7 spouse
ren s1q8 spouse_id
ren s1q10 religion
ren s1q11 born_region
ren s1q12 national
recode s1q13 (0/19=1 "Ashanti-Akan")(60/69=2 "Mole-Dagbon")(30/39=3 "Ewe")(20/29=4 "Ga-Dangme")(50/59=5 "Gurma")(40/49=6 "Guan")(70/79=7 "Grusi")(80/89=8 "Mande")(90/99=9 "Other Groups"), gen(ethnic)
recode ethnic (0=90)(19/20=90)(40=90)(48/50=90)(57=90)(60=90)(70=90)(80=90)(91/94=90)
keep if s1q24==1 // household member
ren hid HID
bys HID nh: egen hhsize = total(s1q24)
keep HID nh pid sex rel borndd bornmm bornyy age agem marital spouse spouse_id born_region national ethnic head hhsize
order HID nh pid sex rel head hhsize borndd bornmm bornyy age agem marital spouse spouse_id born_region national ethnic
save `GLSS7_1'

*** EDUCATION
use "$data\GLSS7_2017\DATA\PartA\g7sec2.dta", clear
ren s2aq1 edu_ever
ren s2aq2 edu_grde7
recode edu_grde7 (0=0 "none")(1/15=1 "preschool")(16/18=2 "primary")(19=3 "lower-secondary") ///
	(20/22=2)(23/25=3)(26/27=4 "upper-secondary")(28/29=2)(30/31=3)(32/34=4)(41/43=5 "post-secondary")(44/46=6 "tertiary")(61=.), gen(edu_lvl)
	replace edu_lvl = 0 if edu_ever == 2
ren s2aq3 edu_qual
ren s2aq5 edu_curr
gen HID = substr(phid,1,8)
keep HID nh pid edu*
save `GLSS7_2' 

*** EMPLOYMENT
use "$data\GLSS7_2017\DATA\PartA\g7sec4.dta", clear
ren s4aq21 work 
recode s4aq4 s4aq6 s4aq9 s4aq12 s4aq19 (1=1)(2=0)
recode s4aq2 s4aq15 (1/2=1)(3=0)
recode s4aq17 (1/3=1)(4=0)
egen jobs = rowtotal(s4aq2 s4aq4 s4aq6 s4aq9 s4aq12 s4aq15 s4aq17 s4aq19), missing
replace jobs = 1 if (jobs == 0 | jobs == .) & work == 1
replace jobs = 0 if work == 2
replace work = 1 if jobs > 0 & jobs != . & work == . 
recode s4aq3 s4aq5 s4aq7 s4aq10 s4aq13 s4aq16 s4aq18 s4aq20 (999=.)
gen hrs = . 
replace hrs = s4aq3  if s4aq36==1
replace hrs = s4aq3  if s4aq36==2
replace hrs = s4aq18 if s4aq36==3
replace hrs = s4aq18 if s4aq36==4
replace hrs = s4aq10 if s4aq36==5
replace hrs = s4aq10 if s4aq36==6
replace hrs = s4aq13 if s4aq36==7
replace hrs = s4aq7  if s4aq36==8
replace hrs = s4aq7  if s4aq36==9
replace hrs = s4aq7  if s4aq36==10
replace hrs = s4aq5  if s4aq36==11
replace hrs = s4aq16 if s4aq36==12 & s4aq16!=.
replace hrs = s4aq20 if s4aq36==12 & s4aq20!=.
gen days = round(hrs,8)
recode days (0=0)(8=1)(16=2)(24=3)(32=4)(40=5)(48=6)(56/168=7)
replace days = 1 if hrs>0 & hrs<8
order jobs hrs days, after(work)
gen isco08_4 = s4aq33c if work == 1
replace isco08_4 = 5212 if isco08_4==7517 // Local Food preparers (raw and cooked)
replace isco08_4 = 5211 if isco08_4==7518 // Local alcoholic and non alcoholic beverages
merge m:1 isco08_4 using "$data\ISCO_08_4.dta"
replace isco08_4 = . if _merge==1
drop if _merge==2
drop isco88_3_title isco08_4_title obs wgt maxwgt _merge
order isco88_3 isco08_4, after(work)
recode s4aq34c (111/499=1    "Agriculture and fishing")                          ///
			 (500/999=3    "Mining")                                           ///
			 (1000/3399=4  "Manufacturing")                                    ///
			 (3500/3999=5  "Electricity, gas and water supply")                ///
			 (4100/4399=6  "Construction")                                     ///
			 (4500/4799=7  "Wholesale and retail trade")                       ///			  
			 (5500/5699=8  "Hotels and restaurants ")                          ///
			 (4900/5399=9  "Transport, storage and communications")            ///			  
			 (5800/6399=9  "Transport, storage and communications")            ///			  
			 (6400/6699=10 "Financial intermediation")                         ///
			 (6800/6899=11 "Real estate, renting and business activities")     ///			  
			 (8400/8499=12 "Public administration and defence")                ///			  
			 (8500/8599=13 "Education")                                        ///			  
			 (8600/8899=14 "Health and social work")                           ///	
			 (6900/7599=15 "Other services")                                   ///		  
			 (7700/8299=15 "Other services")                                   ///	
			 (9000/9399=15 "Other services")                                   ///	
			 (9400/9699=15 "Other services")                                   ///			  			  
			 (9700/9899=16 "Private households")                               ///			  
			 (9900=17      "Extraterritorial organizations and bodies")        ///
			 (9998/9999=.),gen(industry)
order industry, after(isco88_3)
des s4aq45a s4aq47a s4aq49a // amount received (´incl. bonuses, commissions, allowances, or tips)
local i = 1
foreach var in s4aq45 s4aq47 {
	gen aux`i' = .
	replace aux`i' = `var'a * hrs  if `var'b == 1  // hourly
	replace aux`i' = `var'a * days if `var'b == 2  // daily
	replace aux`i' = `var'a        if `var'b == 3  // weekly
	replace aux`i' = `var'a / 2    if `var'b == 4  // forthnightly (2 weeks)
	replace aux`i' = `var'a / 4.33 if `var'b == 5  // monthly
	replace aux`i' = `var'a / 13   if `var'b == 6  // quarterly
	replace aux`i' = `var'a / 26   if `var'b == 7  // half year 
	replace aux`i' = `var'a / 52   if `var'b == 8  // yearly
	local i = `i'+1
	}	
egen earn = rowtotal(aux1 aux2), missing
lab var earn "weekly labour earnings"
order earn, after(industry)
drop aux*
recode s4aq36 (1=1 "paid employee")(2=1)(3=9 "apprentice")(4=9)(5=2 "nagric with employees") ///
	(6=3 "nagric without employees")(7=4 "unpaid fam worker")(8=5 "agric with employees") ///
	(9=6 "agric without employees")(10=7 "agric unpd fwork") (11=8 "domestic employee")(12=10 "other"), gen(status)
order status, after(s4aq36)
ren s4aq35 sector
ren s4aq37 contract
*ren union
ren s4aq39 paid_holiday
ren s4aq40 paid_leave
*ren pension
ren s4aq43 medcare
ren s4aq41 socsec 
*ren numemp
ren hid HID
keep HID nh pid work /*jobs*/ /*hrs*/ isco88_3 isco08_4 industry /*pay pay_amount pay_time*/ earn status sector contract /*union*/ paid_holiday paid_leave /*pension*/ medcare socsec
save `GLSS7_4' 

*** Combine
use `GLSS7_0', clear
merge 1:m HID nh using `GLSS7_1', nogen
merge 1:1 HID nh pid using `GLSS7_2', nogen
merge 1:1 HID nh pid using `GLSS7_4'
drop if _merge==2
drop _merge
merge m:1 isco88_3 using "$data\ISCO_88_3.dta", keepusing(isco88_3) nogen
replace weight=1 if weight==.
gen wave = 7
order wave
save "$data\GLSS7.dta", replace


**************
*** APPEND ***
**************

use "$data\GLSS5.dta", clear
append using "$data\GLSS6.dta" "$data\GLSS7.dta"
merge m:1 year using "$data\CPI.dta"
drop if _merge == 2
drop _merge
replace CPI = CPI/100
ren earn earn_n
gen earn = earn_n/CPI
	lab var earn "weekly labour earnings (constant 2010 LCU)"
	sum earn, d
	replace earn = . if earn>r(p99) & earn !=.
	replace earn = . if earn == 0
gen lnearn = ln(earn)
lab var lnearn "Log weekly labour earnings (constant 2010 LCU)"
order earn lnearn, after(earn_n)
order wave year clust hhid HID nh pid weight region ez urban
*keep if dwell==1
save "$data\GLSS_combined.dta", replace
save "$dataGLSS\GLSS_combined.dta", replace
